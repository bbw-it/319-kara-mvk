plugins {
    id 'java'
    id 'de.undercouch.download' version '5.6.0'
}

repositories {
    mavenCentral()
}

group = 'ch.bbw'
version = '0.0.0-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

def karaJar = file('build/downloads/javakara.jar')
dependencies {
	runtimeOnly files(karaJar)
	testImplementation 'org.junit.jupiter:junit-jupiter:5.12.2'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

def dl = tasks.register("javakaraDownload", Download) { // mavenCentral hello?
    src 'https://www.swisseduc.ch/informatik/karatojava/javakara/classes/java17/javakara-x.jar'
    dest karaJar
    overwrite false
    finalizedBy "javakaraDownloadVerify"
}

tasks.register("javakaraDownloadVerify", Verify) {
    src karaJar
    checksum 'cfc8d58a662037644ec67ac4e519d7fa'
}

tasks.register("run", JavaExec) {
    dependsOn dl
    classpath = files(karaJar)
}

test {
    useJUnitPlatform()
}
